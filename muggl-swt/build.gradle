plugins {
  id "com.google.osdetector" version "1.4.0"
}
apply plugin: 'java'
apply plugin: 'application'

String swtVersion = '4.3.2';

String swtArch = null;
if (osdetector.os == 'linux') {
	if (osdetector.arch == 'x86_64') {
		swtArch = 'gtk.linux.x86_64';
	} else if (osdetector.arch == 'x86_32') {
		swtArch = 'gtk.linux.x86';
	}
} else if (osdetector.os == 'windows') {
	if (osdetector.arch == 'x86_64') {
		swtArch = 'win32.win32.x86_64';
	} else if (osdetector.arch == 'x86_32') {
		swtArch = 'win32.win32.x86';
	}
} else if (osdetector.os == 'osx') {
	if (osdetector.arch == 'x86_64') {
		swtArch = 'cocoa.macosx.x86_64';
	}
}

if (swtArch == null) {
	throw new GradleScriptException("No suitable SWT library found for your platform.")
}

dependencies {
	compile project(':muggl-core')
	compile project(':muggl-common')
	compile 'junit:junit:4.+'
	compile 'org.eclipse.swt:org.eclipse.swt.'+swtArch+':'+swtVersion
}

sourceSets {
	main.java.srcDir "src"
	main.resources.srcDir "images"
	main.java.exclude "test/logicjava/**/*"
}

mainClassName = 'de.wwu.muggl.ui.UiStarter'

eclipse.jdt.file {
	withProperties { jdt ->
		setProperties(jdt)
	}
}
